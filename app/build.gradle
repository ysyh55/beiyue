apply plugin: 'com.android.application'

android {
    signingConfigs {
        myConfig {
            //需要在build.gradle同目录下的gradle.properties中配置以下信息
            //RELEASE_STORE_FILE=/path/to/key/
            //RELEASE_STORE_PASSWORD=***********
            //RELEASE_KEY_ALIAS=*****
            //RELEASE_KEY_PASSWORD=*******
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "me.zheteng.cbreader"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 21
        versionName "1.2.5.0"
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.myConfig
        }
        release {
            signingConfig signingConfigs.myConfig
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-gson.pro'

            def today = new Date()

            applicationVariants.all { variant ->
                if (variant.buildType.getName().equals("release")) {
                    println "~~~~~change the out put apk name"
                    variant.outputs.each { output ->
                        output.outputFile = new File(
                                output.outputFile.parent,
                                output.outputFile.name.replace(".apk", "-" + defaultConfig.versionName + "-" + today.format('yyyyMMdd') + ".apk"))
                    }
                }

            }
        }
    }

    productFlavors {
        google_play {
            manifestPlaceholders = [channel: "google_play"]
            resValue "string", "app_name", "CNBETA Now"
        }
        beiyue {
            manifestPlaceholders = [channel: "beiyue"]
            resValue "string", "app_name", "贝阅"
            applicationId "me.zheteng.cbreader.beiyue"
        }

    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':ActionableToastbar')
    compile project(':IndexableListView')
    compile 'com.mcxiaoke.volley:library:1.0.+'
    compile 'com.android.support:appcompat-v7:22.0.+'
    compile 'com.android.support:cardview-v7:22.0.+'
    compile 'com.android.support:recyclerview-v7:22.0.+'
    compile 'com.android.support:palette-v7:22.0.+'
    compile 'com.google.android.gms:play-services-ads:7.5.0'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.github.ksoichiro:android-observablescrollview:1.5.0'
    compile 'com.umeng.analytics:analytics:5.4.2'
    compile 'com.github.chrisbanes.photoview:library:1.2.3'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'
    compile 'joda-time:joda-time:2.7'
    compile 'org.jsoup:jsoup:1.8.1'
    compile 'com.loopeer.android.thirdparty:swipe-back-layout:1.0'
    compile 'se.emilsjolander:stickylistheaders:2.6.0'
}
